# Define AWS Provider #######################################
# It automatically takes the credentials from
# "~/.aws/credentials" for profile "okta-elastic-dev"
provider "aws" {
  region                   = "us-east-2"
  profile                  = "okta-elastic-dev"
}

module "eks_cluster" {
  source = "../../modules/services/eks-cluster"

  name = "example-eks-cluster"
  min_size = 1
  max_size = 2
  desired_size = 1

  # Due to the way EKS works with ENIs, t3.small is the smallest instance type
  # that can be used for worker nodes. If you try something smaller like t2.micro,
  # which only has 4 ENIs, they´ll all be used up by system services (e.g., kube-proxy)
  # and you won´t be able to deploy your own Pods
  instance_types = [ "t3.small" ]
}

# Authenticate to Kubernetes EKS
provider "kubernetes" {
  host = module.eks_cluster.cluster_endpoint
  cluster_ca_certificate = base64decode(module.eks_cluster.cluster_certificate_authority[0].data)
  token = data.aws_eks_cluster_auth.cluster.token
}

data "aws_eks_cluster_auth" "cluster" {
  name = module.eks_cluster.cluster_name
}

module "simple_webapp" {
  source = "../../modules/services/k8s-app"

  name = "simple-webapp" 
  image = "training/webapp"
  replicas = 2
  container_port = 5000

  environment_variables = {
    "PROVIDER" = "Terraform"
  }

  # Only deploy the app after the cluster has been deployed
  depends_on = [module.eks_cluster]
}